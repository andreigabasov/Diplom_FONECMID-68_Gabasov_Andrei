#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения) 
	
	 // регистр ВКМ_ВзаиморасчетыССотрудниками
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ВКМ_ВзаиморасчетыССотрудниками");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = ВКМ_Выплаты;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ВКМ_Сотрудник", "ВКМ_Сотрудник");
	Блокировка.Заблокировать();
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
				   |	ВКМ_ВыплатаЗарплатыВКМ_Выплаты.ВКМ_Сотрудник КАК Сотрудник,
				   |	СУММА(ВКМ_ВыплатаЗарплатыВКМ_Выплаты.ВКМ_Сумма) КАК Сумма,
				   |	ВКМ_ВыплатаЗарплатыВКМ_Выплаты.ВКМ_ВидРасчета КАК ВидРасчета
				   |ПОМЕСТИТЬ ВТ_Док
				   |ИЗ
				   |	Документ.ВКМ_ВыплатаЗарплаты.ВКМ_Выплаты КАК ВКМ_ВыплатаЗарплатыВКМ_Выплаты
				   |ГДЕ
				   |	ВКМ_ВыплатаЗарплатыВКМ_Выплаты.Ссылка = &Ссылка
				   |СГРУППИРОВАТЬ ПО
				   |	ВКМ_ВыплатаЗарплатыВКМ_Выплаты.ВКМ_Сотрудник,
				   |	ВКМ_ВыплатаЗарплатыВКМ_Выплаты.ВКМ_ВидРасчета
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ
				   |	ВТ_Док.Сотрудник,
				   |	ВТ_Док.ВидРасчета,
				   |	ВТ_Док.Сумма,
				   |	ЕСТЬNULL(ВКМ_ВзаиморасчетыССотрудникамиОстатки.ВКМ_СуммаОстаток, 0) КАК СуммаОстаток
				   |ИЗ
				   |	ВТ_Док КАК ВТ_Док
				   |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВКМ_ВзаиморасчетыССотрудниками.Остатки(&Дата, ВКМ_Сотрудник В
				   |			(ВЫБРАТЬ
				   |				ВТ_Док.Сотрудник КАК Сотрудник
				   |			ИЗ
				   |				ВТ_Док КАК ВТ_Док)) КАК ВКМ_ВзаиморасчетыССотрудникамиОстатки
				   |		ПО ВТ_Док.Сотрудник = ВКМ_ВзаиморасчетыССотрудникамиОстатки.ВКМ_Сотрудник";

	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Дата", МоментВремени());

	Выборка = Запрос.Выполнить().Выбрать();

	Пока Выборка.Следующий() Цикл
		Если Выборка.СуммаОстаток = 0 Тогда
			ОбщегоНазначения.СообщитьПользователю(НСтр(СтрШаблон("ru = 'У Сотрудника %1 начислений нет'", Выборка.Сотрудник)));
			Отказ = Истина;
		ИначеЕсли Выборка.Сумма > Выборка.СуммаОстаток Тогда
			ОбщегоНазначения.СообщитьПользователю(НСтр(СтрШаблон("ru = 'По сотруднику %1 неверная сумма к выплате, начислено %2'", Выборка.Сотрудник, Выборка.СуммаОстаток)));
			Отказ = Истина;
		КонецЕсли;

		Если Отказ Тогда
			Продолжить;
		КонецЕсли;

		Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.ВКМ_Сотрудник = Выборка.Сотрудник;
		Движение.ВКМ_ВидРасчета = Выборка.ВидРасчета;
		Движение.ВКМ_Сумма = Выборка.Сумма;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#КонецЕсли