#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	СформироватьДвиженияДополнительныеНачиления();
	
	СформироватьДвиженияУдержания();
	
	ДвиженияВзаиморасчетыССотрудниками();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьДвиженияДополнительныеНачиления()
	
	Движения.ВКМ_ДополнительныеНачисления.Записывать = Истина;
	
	Для Каждого Строка Из ВКМ_НачисленияПремий Цикл
		
		Движение = Движения.ВКМ_ДополнительныеНачисления.Добавить();
		Движение.ПериодРегистрации = Дата;
		Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_ДополнительныеНачисления.ВКМ_ФиксПремия;
		Движение.ВКМ_Сотрудник = Строка.ВКМ_Сотрудник;
		Движение.БазовыйПериодНачало = НачалоМесяца(Дата);
		Движение.БазовыйПериодКонец = КонецМесяца(Дата);
		Движение.ВКМ_Результат = Строка.ВКМ_СуммаПремии;
		
	КонецЦикла;
	
	Движения.ВКМ_ДополнительныеНачисления.Записать();
	
КонецПроцедуры

Процедура СформироватьДвиженияУдержания()
	
	Движения.ВКМ_Удержания.Записывать = Истина;
	
	Для Каждого Строка Из ВКМ_НачисленияПремий Цикл
		
		Движение = Движения.ВКМ_Удержания.Добавить();
		Движение.ПериодРегистрации = Дата;
		Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.ВКМ_НДФЛ;
		Движение.ВКМ_Сотрудник = Строка.ВКМ_Сотрудник;
		Движение.БазовыйПериодНачало = НачалоМесяца(Дата);
		Движение.БазовыйПериодКонец = КонецМесяца(Дата);
		Движение.ВКМ_Результат = Строка.ВКМ_СуммаПремии * 13 / 100;
		
	КонецЦикла;
	
	Движения.ВКМ_Удержания.Записать();
	
КонецПроцедуры

Процедура ДвиженияВзаиморасчетыССотрудниками()
	
	// регистр ВзаиморасчетыССотрудниками
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВКМ_НачислениеФиксПремийВКМ_НачисленияПремий.ВКМ_Сотрудник КАК Сотрудник,
	|	СУММА(ВКМ_НачислениеФиксПремийВКМ_НачисленияПремий.ВКМ_СуммаПремии) КАК СуммаПремии
	|ИЗ
	|	Документ.ВКМ_НачислениеФиксПремий.ВКМ_НачисленияПремий КАК ВКМ_НачислениеФиксПремийВКМ_НачисленияПремий
	|ГДЕ
	|	ВКМ_НачислениеФиксПремийВКМ_НачисленияПремий.Ссылка = &Ссылка
	|СГРУППИРОВАТЬ ПО
	|	ВКМ_НачислениеФиксПремийВКМ_НачисленияПремий.ВКМ_Сотрудник";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	
	Пока РезультатЗапроса.Следующий() Цикл
		
		Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.ВКМ_Сотрудник = РезультатЗапроса.Сотрудник;
		Движение.ВКМ_ВидРасчета = ПланыВидовРасчета.ВКМ_ДополнительныеНачисления.ВКМ_ФиксПремия;
		СуммаУдержания = РезультатЗапроса.СуммаПремии * 13 / 100;
		Движение.ВКМ_Сумма = РезультатЗапроса.СуммаПремии - СуммаУдержания;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли