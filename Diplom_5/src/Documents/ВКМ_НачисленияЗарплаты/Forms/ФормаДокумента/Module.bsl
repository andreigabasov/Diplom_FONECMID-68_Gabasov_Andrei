

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	Объект.Дата = КонецМесяца(Объект.Дата);
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	ЗаполнитьНаСервере();
КонецПроцедуры

#КонецОбласти  

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	Объект.ВКМ_НачисленияЗП.Очистить();
	
	Дата = Объект.Дата;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВКМ_ОбслуживаниеКлиента.ВКМ_Специалист КАК ВКМ_Специалист,
		|	ВКМ_ОбслуживаниеКлиента.Ссылка КАК ВКМ_ОбслуживаниеКлиента
		|ПОМЕСТИТЬ ВТ_ПроцентОтРабот
		|ИЗ
		|	Документ.ВКМ_ОбслуживаниеКлиента КАК ВКМ_ОбслуживаниеКлиента
		|ГДЕ
		|	ГОД(ВКМ_ОбслуживаниеКлиента.Дата) = ГОД(&Дата)
		|	И МЕСЯЦ(ВКМ_ОбслуживаниеКлиента.Дата) = МЕСЯЦ(&Дата)
		|	И ВКМ_ОбслуживаниеКлиента.Проведен
		|	И НЕ ВКМ_ОбслуживаниеКлиента.ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВКМ_ГрафикОтпусковОтпускаСотрудников.ВКМ_Сотрудник КАК ВКМ_Сотрудник,
		|	ВКМ_ГрафикОтпусковОтпускаСотрудников.ВКМ_ДатаНачала КАК ВКМ_ДатаНачала,
		|	ВКМ_ГрафикОтпусковОтпускаСотрудников.ВКМ_ДатаОкончания КАК ВКМ_ДатаОкончания
		|ПОМЕСТИТЬ ВТ_ДанныеОтпусков
		|ИЗ
		|	Документ.ВКМ_ГрафикОтпусков.ОтпускаСотрудников КАК ВКМ_ГрафикОтпусковОтпускаСотрудников
		|ГДЕ
		|	(МЕСЯЦ(ВКМ_ГрафикОтпусковОтпускаСотрудников.ВКМ_ДатаНачала) = МЕСЯЦ(&Дата)
		|				И ГОД(ВКМ_ГрафикОтпусковОтпускаСотрудников.ВКМ_ДатаНачала) = ГОД(&Дата)
		|			ИЛИ МЕСЯЦ(ВКМ_ГрафикОтпусковОтпускаСотрудников.ВКМ_ДатаОкончания) = МЕСЯЦ(&Дата)
		|				И ГОД(ВКМ_ГрафикОтпусковОтпускаСотрудников.ВКМ_ДатаОкончания) = ГОД(&Дата))
		|	И ВКМ_ГрафикОтпусковОтпускаСотрудников.Ссылка.Проведен
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.ВКМ_Сотрудник КАК ВКМ_Сотрудник,
		|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.ВКМ_Оклад КАК ВКМ_Оклад,
		|	ЕСТЬNULL(ВТ_ДанныеОтпусков.ВКМ_ДатаНачала, НЕОПРЕДЕЛЕНО) КАК ВКМ_ДатаНачалаОтпуска,
		|	ЕСТЬNULL(ВТ_ДанныеОтпусков.ВКМ_ДатаОкончания, НЕОПРЕДЕЛЕНО) КАК ВКМ_ДатаОкончанияОтпуска,
		|	ЕСТЬNULL(ВТ_ПроцентОтРабот.ВКМ_ОбслуживаниеКлиента, НЕОПРЕДЕЛЕНО) КАК ВыполненныеРаботы
		|ИЗ
		|	РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&Дата, ) КАК ВКМ_УсловияОплатыСотрудниковСрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПроцентОтРабот КАК ВТ_ПроцентОтРабот
		|		ПО ВКМ_УсловияОплатыСотрудниковСрезПоследних.ВКМ_Сотрудник = ВТ_ПроцентОтРабот.ВКМ_Специалист
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДанныеОтпусков КАК ВТ_ДанныеОтпусков
		|		ПО ВКМ_УсловияОплатыСотрудниковСрезПоследних.ВКМ_Сотрудник = ВТ_ДанныеОтпусков.ВКМ_Сотрудник";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		//Оклад
		СтрокаТЧ = Объект.ВКМ_НачисленияЗП.Добавить();
		СтрокаТЧ.ВКМ_Сотрудник = Выборка.ВКМ_Сотрудник;
		СтрокаТЧ.ВКМ_ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.ВКМ_Оклад;
		СтрокаТЧ.ВКМ_ДатаНачало = НачалоМесяца(Дата);
		СтрокаТЧ.ВКМ_ДатаОкончание = КонецМесяца(Дата);
		СтрокаТЧ.ВКМ_ГрафикРаботы = Объект.ВКМ_ГрафикРаботы;
		
		Если ЗначениеЗаполнено(Выборка.ВыполненныеРаботы) Тогда
			СтрокаТЧ = Объект.ВКМ_НачисленияЗП.Добавить();
			СтрокаТЧ.ВКМ_Сотрудник = Выборка.ВКМ_Сотрудник;
			СтрокаТЧ.ВКМ_ВидРасчета = ПланыВидовРасчета.ВКМ_ДополнительныеНачисления.ВКМ_ПроцентОтРабот;
			СтрокаТЧ.ВКМ_ДатаНачало = НачалоМесяца(Дата);
			СтрокаТЧ.ВКМ_ДатаОкончание = КонецМесяца(Дата);
			СтрокаТЧ.ВКМ_ГрафикРаботы = Объект.ВКМ_ГрафикРаботы;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.ВКМ_ДатаНачалаОтпуска) Тогда
			
			СтрокаТЧ = Объект.ВКМ_НачисленияЗП.Добавить();
			СтрокаТЧ.ВКМ_Сотрудник = Выборка.ВКМ_Сотрудник;
			СтрокаТЧ.ВКМ_ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.ВКМ_Отпуск;
			СтрокаТЧ.ВКМ_ГрафикРаботы = Объект.ВКМ_ГрафикРаботы;
			
			Если Месяц(Дата) = Месяц(Выборка.ВКМ_ДатаНачалаОтпуска) Тогда
				СтрокаТЧ.ВКМ_ДатаНачало = Выборка.ВКМ_ДатаНачалаОтпуска;
			Иначе
				СтрокаТЧ.ВКМ_ДатаНачало = НачалоМесяца(Дата);
			КонецЕсли;
			
			Если Месяц(Дата) = Месяц(Выборка.ВКМ_ДатаОкончанияОтпуска) Тогда
				СтрокаТЧ.ВКМ_ДатаОкончание = Выборка.ВКМ_ДатаОкончанияОтпуска;
			Иначе
				СтрокаТЧ.ВКМ_ДатаОкончание = КонецМесяца(Дата);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
























