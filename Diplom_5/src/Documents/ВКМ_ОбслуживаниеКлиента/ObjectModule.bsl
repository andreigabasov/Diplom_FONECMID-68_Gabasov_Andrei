
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий


Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты) 
	
	Данные = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВКМ_Договор, "ВидДоговора, ВКМ_ДатаНачала, ВКМ_ДатаОкончания"); 
	
	Если Данные.ВидДоговора <> ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.ВКМ_Абонент") Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Проведение документа возможно только с абонентским договором'"));
		Отказ = Истина
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(Данные.ВКМ_ДатаНачала) и ЗначениеЗаполнено(Данные.ВКМ_ДатаОкончания) Тогда
		Если Дата < Данные.ВКМ_ДатаНачала Или Дата > Данные.ВКМ_ДатаОкончания Тогда
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Дата документа за границами периода действия договора'"));
			Отказ = Истина
		КонецЕсли;
	Иначе
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Период проведения работ не заполнен'"));
		Отказ = Истина
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	СУММА(ВКМ_ОбслуживаниеКлиентаВКМ_ВыполненныеРаботы.ВКМ_ЧасыКОплате) КАК ЧасыКОплате,
	|	ВКМ_ОбслуживаниеКлиентаВКМ_ВыполненныеРаботы.Ссылка.ВКМ_Договор.ВКМ_СтавкаЧаса КАК СтавкаЧаса,
	|	ВКМ_ОбслуживаниеКлиентаВКМ_ВыполненныеРаботы.Ссылка.ВКМ_Специалист КАК Специалист
	|ПОМЕСТИТЬ ВТ_Документ
	|ИЗ
	|	Документ.ВКМ_ОбслуживаниеКлиента.ВКМ_ВыполненныеРаботы КАК ВКМ_ОбслуживаниеКлиентаВКМ_ВыполненныеРаботы
	|ГДЕ
	|	ВКМ_ОбслуживаниеКлиентаВКМ_ВыполненныеРаботы.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ВКМ_ОбслуживаниеКлиентаВКМ_ВыполненныеРаботы.Ссылка.ВКМ_Договор.ВКМ_СтавкаЧаса,
	|	ВКМ_ОбслуживаниеКлиентаВКМ_ВыполненныеРаботы.Ссылка.ВКМ_Специалист
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.ВКМ_Сотрудник КАК Сотрудник,
	|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.ВКМ_ПроцентОтРабот КАК ПроцентОтРабот
	|ПОМЕСТИТЬ ВТ_УсловияОплаты
	|ИЗ
	|	РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&Дата, ВКМ_Сотрудник = &Специалист) КАК ВКМ_УсловияОплатыСотрудниковСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Документ.ЧасыКОплате КАК ЧасыКОплате,
	|	ЕстьNull(ВТ_УсловияОплаты.ПроцентОтРабот, """") КАК ПроцентОтРабот,
	|	ВТ_Документ.СтавкаЧаса КАК СтавкаЧаса
	|ИЗ
	|	ВТ_Документ КАК ВТ_Документ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_УсловияОплаты КАК ВТ_УсловияОплаты
	|		ПО ВТ_Документ.Специалист = ВТ_УсловияОплаты.Сотрудник";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Специалист", ВКМ_Специалист);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ПроцентОтРабот = "" Тогда
			ОбщегоНазначения.СообщитьПользователю(НСтр(СтрШаблон("ru = 'У сотрудника %1 не указан %% от работ'", ВКМ_Специалист)));
			Отказ = Истина;
			Возврат;
		Иначе
			
			//регистр ВКМ_ВыполненныеКлиентуРаботы 
			Движения.ВКМ_ВыполненныеКлиентуРаботы.Записывать = Истина;
			Движение = Движения.ВКМ_ВыполненныеКлиентуРаботы.Добавить();
			Движение.Период = Дата;
			Движение.ВКМ_Клиент = ВКМ_Клиент;
			Движение.ВКМ_Договор = ВКМ_Договор;
			Движение.ВКМ_КоличествоЧасов = Выборка.ЧасыКОплате;
			Движение.ВКМ_СуммаКОплате = Выборка.ЧасыКОплате * Выборка.СтавкаЧаса;
			
			//регистр ВКМ_ВыполненныеСотрудникомРаботы
			Движения.ВКМ_ВыполненныеСотрудникомРаботы.Записывать = Истина;
			Движение = Движения.ВКМ_ВыполненныеСотрудникомРаботы.Добавить();
			Движение.Период = Дата;
			Движение.ВКМ_Сотрудник = ВКМ_Специалист;
			Движение.ВКМ_ЧасовКОплате = Выборка.ЧасыКОплате;
			Движение.ВКМ_СуммаКОплате = (Выборка.ЧасыКОплате * Выборка.СтавкаЧаса * Выборка.ПроцентОтРабот) / 100;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыОбъекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "ВКМ_ДатаПроведенияРабот, ВКМ_ВремяНачалаРабот, ВКМ_Специалист");
	
	ЗаказИзменен = Ложь;
	
	Если РеквизитыОбъекта.ВКМ_ДатаПроведенияРабот <> ВКМ_ДатаПроведенияРабот
		Или РеквизитыОбъекта.ВКМ_ВремяНачалаРабот <> ВКМ_ВремяНачалаРабот
		Или РеквизитыОбъекта.ВКМ_Специалист <> ВКМ_Специалист Тогда 
		ЗаказИзменен = Истина;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ЗаказИзменен", ЗаказИзменен);
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДатаПроведенияРабот = Формат(ВКМ_ДатаПроведенияРабот, "ДЛФ=D");
	ДатаНачала = Формат(ВКМ_ВремяНачалаРабот, "ДЛФ=T");
	ДатаОкончания = Формат(ВКМ_ВремяОкончанияРабот, "ДЛФ=T");
	
	Если ДополнительныеСвойства.ЭтоНовый Тогда
		
		Сообщение = НСтр(СтрШаблон("ru = 'НОВЫЙ. Заказ № %1. Сотрудник %2. Начало работ %3. Период проведения работ %4 - %5'", 
			Номер, ВКМ_Специалист, ДатаПроведенияРабот, ДатаНачала, ДатаОкончания));
		
		УведомлениеОбъект = Справочники.ВКМ_УведомленияТГБоту.СоздатьЭлемент();
		УведомлениеОбъект.ВКМ_ТекстСоббщения = Сообщение;
		
		Попытка
			УведомлениеОбъект.Записать();
		Исключение
			ЖурналРегистрации.ДобавитьСообщениеДляЖурналаРегистрации(
			"ПриЗаписи",
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Документы.ВКМ_ОбслуживаниеКлиента,
			Неопределено,
			СтрШаблон("Ошибка: %1", ОписаниеОшибки()));
			Отказ = Истина;
		КонецПопытки;
		
	ИначеЕсли ДополнительныеСвойства.ЗаказИзменен Тогда
		
		Сообщение = НСтр(СтрШаблон("ru = 'ИЗМЕНЕНИЯ. Заказ № %1. Сотрудник %2. Начало работ %3. Период проведения работ %4 - %5'", 
			Номер, ВКМ_Специалист, ДатаПроведенияРабот, ДатаНачала, ДатаОкончания));
		
		УведомлениеОбъект = Справочники.ВКМ_УведомленияТГБоту.СоздатьЭлемент();
		УведомлениеОбъект.ВКМ_ТекстСоббщения = Сообщение; 
		
		Попытка
			УведомлениеОбъект.Записать();
		Исключение
			ЖурналРегистрации.ДобавитьСообщениеДляЖурналаРегистрации(
			"ПриЗаписи",
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Документы.ВКМ_ОбслуживаниеКлиента,
			Неопределено,
			СтрШаблон("Ошибка: %1", ОписаниеОшибки()));
			Отказ = Истина;
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли