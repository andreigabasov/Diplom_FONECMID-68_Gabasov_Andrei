#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты) 
	
	Для каждого Строка Из ОтпускаСотрудников Цикл   
		
		Если ЗначениеЗаполнено(Строка.ВКМ_ДатаНачала) И ЗначениеЗаполнено(Строка.ВКМ_ДатаОкончания) Тогда 
			Если Строка.ВКМ_ДатаНачала > Строка.ВКМ_ДатаОкончания Тогда  
				ОбщегоНазначения.СообщитьПользователю(НСтр(СтрШаблон("ru = 'В строке №%1 не верно заполнен период'", Строка.НомерСтроки)));
				Отказ = Истина; 
			КонецЕсли; 
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ВКМ_УчетОтпусков.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВКМ_ГрафикОтпусковВКМ_ОтпускаСотрудников.ВКМ_Сотрудник КАК Сотрудник,
	|	ВКМ_ГрафикОтпусковВКМ_ОтпускаСотрудников.ВКМ_ДатаОкончания КАК ДатаОкончания,
	|	РАЗНОСТЬДАТ(ВКМ_ГрафикОтпусковВКМ_ОтпускаСотрудников.ВКМ_ДатаНачала,
	|		ВКМ_ГрафикОтпусковВКМ_ОтпускаСотрудников.ВКМ_ДатаОкончания, ДЕНЬ) + 1 КАК КоличествоДней
	|ИЗ
	|	Документ.ВКМ_ГрафикОтпусков.ОтпускаСотрудников КАК ВКМ_ГрафикОтпусковВКМ_ОтпускаСотрудников
	|ГДЕ
	|	ВКМ_ГрафикОтпусковВКМ_ОтпускаСотрудников.Ссылка = &Ссылка
	|ИТОГИ
	|	СУММА(КоличествоДней)
	|ПО
	|	Сотрудник";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	БазаОтпуска = 28;
	
	Пока Выборка.Следующий() Цикл
		
		ПревышеноДней = Выборка.КоличествоДней - БазаОтпуска;
		
		Если ПревышеноДней > 0 Тогда
			
			ОбщегоНазначения.СообщитьПользователю(НСтр(СтрШаблон("ru = 'Превышено количество дней отпуска в год по сотруднику %1'", Выборка.Сотрудник)));
			
		КонецЕсли;
		
		РезультатДетально = Выборка.Выбрать();
		
		Пока РезультатДетально.Следующий() Цикл
			
			Движение = Движения.ВКМ_УчетОтпусков.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Период = РезультатДетально.ДатаОкончания;
			Движение.ВКМ_Сотрудник = РезультатДетально.Сотрудник;
			Движение.ВКМ_Значение = РезультатДетально.КоличествоДней;
			
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры 

#КонецОбласти

#КонецЕсли
