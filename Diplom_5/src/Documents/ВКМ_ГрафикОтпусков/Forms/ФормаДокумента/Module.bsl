#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	КолвоДнейОтпуска();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОтпускаСотрудников

&НаКлиенте
Процедура ВКМ_ОтпускаСотрудниковВКМ_ДатаОкончанияПриИзменении(Элемент)
	КолвоДнейОтпуска();
КонецПроцедуры

&НаКлиенте
Процедура ВКМ_ОтпускаСотрудниковВКМ_ДатаНачалаПриИзменении(Элемент)
	КолвоДнейОтпуска();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы 

&НаКлиенте
Процедура АнализГрафика(Команда)

	АдресВХранилище = ПоместитьВоВременноеХранилищеНаСервере();
	ПараметрыФормы = Новый Структура("АдресВХранилище", АдресВХранилище);
	ОткрытьФорму("Документ.ВКМ_ГрафикОтпусков.Форма.АнализГрафика", ПараметрыФормы, ЭтотОбъект);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПоместитьВоВременноеХранилищеНаСервере()

	Адрес = ПоместитьВоВременноеХранилище(Объект.ОтпускаСотрудников.Выгрузить( ,"ВКМ_Сотрудник,ВКМ_ДатаНачала,ВКМ_ДатаОкончания"),);
	Возврат Адрес;

КонецФункции

&НаКлиенте
Процедура КолвоДнейОтпуска()

	МассивСотрудники = ВКМ_ПолучитьКоличествоДнейОтпуска();
	УстановитьПодсветкуСтрок(МассивСотрудники);

КонецПроцедуры

&НаСервере
Процедура УстановитьПодсветкуСтрок(МассивСотрудники)
	
	УсловноеОформление.Элементы.Очистить();
	Если МассивСотрудники.Количество() > 0 Тогда
		Для Каждого Сотрудник Из МассивСотрудники Цикл
			ЭлементОформления = УсловноеОформление.Элементы.Добавить();
			
			ЭлементОформления.Использование = Истина;
			ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", WebЦвета.ЛососьСветлый);
			ЭлементУсловия = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ЭлементУсловия.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ОтпускаСотрудников.ВКМ_Сотрудник");
			ЭлементУсловия.ПравоеЗначение = Сотрудник;
			ЭлементУсловия.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			ЭлементУсловия.Использование = Истина;
			
			ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
			ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ВКМ_ОтпускаСотрудниковВКМ_Сотрудник");
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ВКМ_ПолучитьКоличествоДнейОтпуска()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВложенныйЗапрос.ВКМ_Сотрудник КАК Сотрудник,
	|	ВложенныйЗапрос.ВКМ_ДатаНачала КАК ДатаНачала,
	|	ВложенныйЗапрос.ВКМ_ДатаОкончания КАК ДатаОкончания
	|ПОМЕСТИТЬ ВТ_ДанныеДокумента
	|ИЗ
	|	&МассивТаблица КАК ВложенныйЗапрос
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеДокумента.Сотрудник КАК Сотрудник,
	|	СУММА(РАЗНОСТЬДАТ(ВТ_ДанныеДокумента.ДатаНачала, ВТ_ДанныеДокумента.ДатаОкончания, ДЕНЬ) + 1) КАК КоличествоДней
	|ИЗ
	|	ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ДанныеДокумента.Сотрудник";
	
	Запрос.УстановитьПараметр("МассивТаблица", Объект.ОтпускаСотрудников.Выгрузить());
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	МассивСотрудники = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.КоличествоДней > 28 Тогда
			МассивСотрудники.Добавить(Выборка.Сотрудник);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат МассивСотрудники;
	
КонецФункции

#КонецОбласти