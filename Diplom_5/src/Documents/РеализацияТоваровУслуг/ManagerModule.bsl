
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.РеализацияТоваровУслуг) Тогда
		
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.РеализацияТоваровУслуг.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначения.ПредставлениеОбъекта(Метаданные.Документы.РеализацияТоваровУслуг);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции 

//andrei↓↓↓ПечатьАкта
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Акт оказанных услуг
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АктОбОказанииУслуг";
	КомандаПечати.Представление = НСтр("ru = 'Акт об оказании услуг'");
	КомандаПечати.Порядок = 5;

КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт

	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "АктОбОказанииУслуг");
	Если ПечатнаяФорма <> Неопределено Тогда
		ПечатнаяФорма.ТабличныйДокумент = ПечатьАкта(МассивОбъектов, ОбъектыПечати);
		ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Акт об оказании услуг'");
		ПечатнаяФорма.ПолныйПутьКМакету = "Документ.РеализацияТоваровУслуг.ВКМ_ПФ_MXL_Акт";
	КонецЕсли;

КонецПроцедуры
//andrei↑↑↑ПечатьАкта

#КонецОбласти 

//andrei↓↓↓ПечатьАкта
#Область СлужебныеПроцедурыИФункции

Функция ПечатьАкта(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_Акт";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.РеализацияТоваровУслуг.ВКМ_ПФ_MXL_Акт");
	
	ДанныеДокументов = ПолучитьДанныеДокументов(МассивОбъектов);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеДокументов.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ВывестиШапкуДокумента(ДанныеДокументов, ТабличныйДокумент, Макет);
		
		ВывестиТаблицу(ДанныеДокументов, ТабличныйДокумент, Макет);
		
		ВывестиПодвалДокумента(ДанныеДокументов, ТабличныйДокумент, Макет);
		
		// В табличном документе необходимо задать имя области, в которую был 
		// выведен объект. Нужно для возможности печати комплектов документов.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати,
		ДанныеДокументов.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ПолучитьДанныеДокументов(МассивОбъектов)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Дата,
	|	РеализацияТоваровУслуг.Номер,
	|	РеализацияТоваровУслуг.Ссылка,
	|	РеализацияТоваровУслуг.Организация,
	|	РеализацияТоваровУслуг.Контрагент,
	|	РеализацияТоваровУслуг.Договор,
	|	РеализацияТоваровУслуг.СуммаДокумента,
	|	РеализацияТоваровУслуг.Основание,
	|	РеализацияТоваровУслуг.Ответственный,
	|	РеализацияТоваровУслуг.Комментарий,
	|	РеализацияТоваровУслуг.Услуги.(
	|		Ссылка,
	|		НомерСтроки,
	|		Номенклатура,
	|		Количество,
	|		Цена,
	|		Сумма)
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка В (&МассивДокументов)";
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

Процедура ВывестиШапкуДокумента(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьШапка = Макет.ПолучитьОбласть("ОбластьШапка");
	
	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("МеткаНомер", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеДокументов.Номер));
	ДанныеЗаполнения.Вставить("МеткаДата", Формат(ДанныеДокументов.Дата, "ДЛФ=DD"));
	ДанныеЗаполнения.Вставить("МеткаКонтрагент", ДанныеДокументов.Контрагент);
	ДанныеЗаполнения.Вставить("МеткаОрганизация", ДанныеДокументов.Организация);
	ДанныеЗаполнения.Вставить("МеткаДоговор", ДанныеДокументов.Договор);
	ДанныеЗаполнения.Вставить("МеткаОтветственный", ДанныеДокументов.Ответственный);

	ОбластьШапка.Параметры.Заполнить(ДанныеЗаполнения);
	
	ТабличныйДокумент.Вывести(ОбластьШапка);
	
КонецПроцедуры

Процедура ВывестиТаблицу(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьТаблицаШапка = Макет.ПолучитьОбласть("ОбластьТаблицаШапка");
	ТабличныйДокумент.Вывести(ОбластьТаблицаШапка); 
	
	ОбластьТаблицаСтрока = Макет.ПолучитьОбласть("ОбластьТаблицаСтрока");
	
	Выборка = ДанныеДокументов.Услуги.Выбрать();
	Пока Выборка.Следующий() Цикл	
		ДанныеЗаполнения = Новый Структура;
		ДанныеЗаполнения.Вставить("МеткаНомерСтроки", Выборка.НомерСтроки);
		ДанныеЗаполнения.Вставить("МеткаНоменклатура", Выборка.Номенклатура);
		ДанныеЗаполнения.Вставить("МеткаКоличество", Выборка.Количество);
		ДанныеЗаполнения.Вставить("МеткаЦена", Выборка.Цена);
		ДанныеЗаполнения.Вставить("МеткаСумма", Выборка.Сумма);
		
		ОбластьТаблицаСтрока.Параметры.Заполнить(ДанныеЗаполнения);
		ТабличныйДокумент.Вывести(ОбластьТаблицаСтрока); 
	КонецЦикла; 
	
	ОбластьТаблицаПодвал = Макет.ПолучитьОбласть("ОбластьТаблицаПодвал");
	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("МеткаИтого", ДанныеДокументов.СуммаДокумента);
	ДанныеЗаполнения.Вставить("МеткаСуммаПрописью", ЧислоПрописью(ДанныеДокументов.СуммаДокумента, "Л=ru_RU;ДП=Истина","рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 2"));
	ОбластьТаблицаПодвал.Параметры.Заполнить(ДанныеЗаполнения);
	ТабличныйДокумент.Вывести(ОбластьТаблицаПодвал);
	
КонецПроцедуры

Процедура ВывестиПодвалДокумента(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьПодвал = Макет.ПолучитьОбласть("ОбластьПодвал");
	
	ТабличныйДокумент.Вывести(ОбластьПодвал);
	
КонецПроцедуры

#КонецОбласти
 //andrei↑↑↑ПечатьАкта 
 
#КонецЕсли