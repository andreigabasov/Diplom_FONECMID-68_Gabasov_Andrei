#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция СозданиеСпискаНаСервере(Дата, СоздатьОбъект) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Владелец КАК Контрагент,
	|	ДоговорыКонтрагентов.Ссылка КАК Ссылка,
	|	ДоговорыКонтрагентов.Организация КАК Организация
	|ПОМЕСТИТЬ ВТ_ДанныеДоговора
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	&Дата МЕЖДУ ДоговорыКонтрагентов.ВКМ_ДатаНачала И ДоговорыКонтрагентов.ВКМ_ДатаОкончания
	|	И ДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.ВКМ_Абонент)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	|	РеализацияТоваровУслуг.Контрагент КАК Контрагент,
	|	РеализацияТоваровУслуг.Договор КАК Договор
	|ПОМЕСТИТЬ ВТ_ДанныеРеализации
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Дата МЕЖДУ &ДатаНачало И &ДатаОкончание
	|	И НЕ РеализацияТоваровУслуг.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеДоговора.Ссылка КАК Договор,
	|	ЕСТЬNULL(ВТ_ДанныеРеализации.Ссылка, НЕОПРЕДЕЛЕНО) КАК Реализация,
	|	ВТ_ДанныеДоговора.Организация КАК Организация,
	|	ВТ_ДанныеДоговора.Контрагент КАК Контрагент
	|ИЗ
	|	ВТ_ДанныеДоговора КАК ВТ_ДанныеДоговора
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДанныеРеализации КАК ВТ_ДанныеРеализации
	|		ПО ВТ_ДанныеДоговора.Ссылка = ВТ_ДанныеРеализации.Договор";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("ДатаНачало", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("ДатаОкончание", КонецМесяца(Дата));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	МассивРеализаций = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		РеализацииСтруктура = Новый Структура;
		Если СоздатьОбъект Тогда
			Если НЕ ЗначениеЗаполнено(Выборка.Реализация) Тогда
				НоваяРеализация = СозданиеРеализации(Выборка, КонецМесяца(Дата));
				РеализацииСтруктура.Вставить("Договор", Выборка.Договор);
				РеализацииСтруктура.Вставить("Реализация", НоваяРеализация);
			Иначе
				РеализацииСтруктура.Вставить("Договор", Выборка.Договор);
				РеализацииСтруктура.Вставить("Реализация", Выборка.Реализация);
			КонецЕсли;
		Иначе
			РеализацииСтруктура.Вставить("Договор", Выборка.Договор);
			РеализацииСтруктура.Вставить("Реализация", Выборка.Реализация);
		КонецЕсли;
		
		МассивРеализаций.Добавить(РеализацииСтруктура);
		
	КонецЦикла;
	
	Возврат МассивРеализаций;
	
КонецФункции

Функция СозданиеРеализации(Выборка, Дата)
	
	ДокументОбъект = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
	ДокументОбъект.Дата = Дата;
	ДокументОбъект.Договор = Выборка.Договор;
	ДокументОбъект.Контрагент = Выборка.Контрагент;
	ДокументОбъект.Организация = Выборка.Организация;
	ДокументОбъект.ВКМ_ВыполнитьАвтозаполнение();
	ДокументОбъект.Ответственный = Пользователи.ТекущийПользователь();
	
	Если ДокументОбъект.ПроверитьЗаполнение() Тогда
		
		Попытка
			НачатьТранзакцию();
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ОбщегоНазначения.СообщитьПользователю(НСтр(СтрШаблон("ru = 'Не удалось создать реализацию по договору %1'", Выборка.Договор)));
			ЗаписьЖурналаРегистрации("ОБРАБОТКА: Массовое создание актов отменено");
			ЖурналРегистрации.ДобавитьСообщениеДляЖурналаРегистрации(
			"СозданиеРеализации",
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Обработки.ВКМ_МассовоеСозданиеАктов,
			Неопределено,
			СтрШаблон("Ошибка: %1", ОписаниеОшибки()));

		КонецПопытки;
		
	Иначе
		ОбщегоНазначения.СообщитьПользователю(НСтр(СтрШаблон("ru = 'Не удалось создать документ реализации по Договору: %1. Не все обязательные данные заполнены'", Выборка.Договор)));
	КонецЕсли;
	
	Возврат ДокументОбъект.Ссылка;
	
КонецФункции

#КонецОбласти

#КонецЕсли